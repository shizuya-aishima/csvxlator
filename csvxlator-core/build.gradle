plugins {
	id 'java-library'
	id 'maven-publish'
	id 'jacoco'
}

group = 'io.github.shizuya-aishima'
version = '0.1.0'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.apache.poi:poi:5.2.5'
	implementation 'org.apache.poi:poi-ooxml:5.4.0'
	implementation 'org.apache.commons:commons-csv:1.10.0'
	implementation 'org.slf4j:slf4j-api:2.0.9'
	implementation 'org.slf4j:slf4j-simple:2.0.16'

	testImplementation platform('org.junit:junit-bom:5.11.4')
	testImplementation 'org.junit.jupiter:junit-jupiter'
	testImplementation 'org.assertj:assertj-core:3.27.3'
}

java {
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
	withJavadocJar()
	withSourcesJar()
}

tasks.named('jar') {
	manifest {
		attributes(
				'Implementation-Title': project.name,
				'Implementation-Version': project.version,
				'Built-By': System.getProperty('user.name'),
				'Built-Date': new Date(),
				'Built-JDK': System.getProperty('java.version')
				)
	}
}

test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
		html.required = true
	}
}

publishing {
	repositories {
		maven {
			name = "GitHubPackages"
			url = uri("https://maven.pkg.github.com/${System.getenv('GITHUB_REPOSITORY') ?: 'shizuya-aishima/csvxlator'}")
			credentials {
				username = System.getenv("GITHUB_ACTOR")
				password = System.getenv("GITHUB_TOKEN")
			}
		}
	}
	publications {
		maven(MavenPublication) {
			from components.java
			pom {
				name = 'csvxlator-core'
				description = 'CSVファイルをExcelファイルに変換する軽量で柔軟なライブラリ'
				url = 'https://github.com/shizuya-aishima/csvxlator'
				licenses {
					license {
						name = 'MIT License'
						url = 'https://opensource.org/licenses/MIT'
					}
				}
				developers {
					developer {
						id = 'shizuya-aishima'
						name = 'Shizuya Aishima'
						email = 'shizuya.aishima@example.com'
					}
				}
				scm {
					connection = 'scm:git:git://github.com/shizuya-aishima/csvxlator.git'
					developerConnection = 'scm:git:ssh://github.com/shizuya-aishima/csvxlator.git'
					url = 'https://github.com/shizuya-aishima/csvxlator'
				}
			}
		}
	}
}